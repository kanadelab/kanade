＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃
＃
＃　　　奏でる日常の音色　イベント処理
＃
＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃


＃通常サーフェスに変更する指示
＃メニューボードの設定ができたらここに注意だ
＊OnSurfaceRestore
\0（０）\1（（デフォルトサーフェス1））



＃インストール成功
＊OnInstallComplete
＄インストールアイテム	（Ｒ１）
＞舞い散る桜の詩インストール	（Ｒ１）==舞い散る桜の詩&&（桜バルーン変更）==有効
＞インストール完了（Ｒ０）
＞インストール完了その他



＊インストール失敗artificial
＄桜バルーン変更	
：（０）やめちゃう？


＊舞い散る桜の詩インストール
＄桜バルーン変更	
：\![change,balloon,舞い散る桜の詩]

＃＊OnBalloonChange	（compare,（Ｒ０）,舞い散る桜の詩）
＃：（５）どう？\w7
＃お花見気分になるといいな。

＃＊OnBalloonChange	（compare,（Ｒ０）,弾ける花火の舞）
＃：（５）花火、\w3だけどちょっぴり涼しげに。\w7
＃どうかな？

＃＊OnBalloonChange	（compare,（Ｒ０）,焦がれる紅葉の夢）
＃：（２５）気分は紅葉狩りだね。




＃別モードにも突っ込んでおくこと
＊アップデート失敗ログ作成
＄nop	（list,push_front,ねとわく更新エラーログ発生日時,（現在年）年（現在月）月（現在日）日　（現在時）時（現在分）分（現在秒）秒）
＄nop	（list,push_front,ねとわく更新エラーログ要因,（Ｒ０））
＄nop	（when,（list,size,ねとわく更新エラーログ発生日時）>5,（list,pop_back,ねとわく更新エラーログ発生日時））
＄nop	（when,（list,size,ねとわく更新エラーログ要因）>5,（list,pop_back,ねとわく更新エラーログ要因））


＊OnUpdateComplete
＄nop	（辞書フォルダ設定）
＄nop	（list,clear,ねとわく更新エラーログ発生日時）
＄nop	（list,clear,ねとわく更新エラーログ要因）
＄nop	（list,clear,選択肢エラーログ発生日時）
＄nop	（list,clear,選択肢エラーログラベル）
＞ネットワーク更新（Ｒ０）
：（０）成功したけど「（Ｒ０）」だってさ。

＊ネットワーク更新timeout
：（４）タイムアウトだって…
＃＞ねとわくエラー報告表示

＊ネットワーク更新artificial
：（０）ん、\w3中断？

＊ネットワーク更新404
：（４）更新ファイルが見つからないんだって…

＊ネットワーク更新changed
＄nop	（core,set_shell,（現在のシェルのパス））
＄デフォルトサーフェス1	（core,translate_num,（メニューボードサーフェス））
＄更新チェック	更新なし
＞OnFirstBoot	（旧バージョン引き継ぎ）==有効
：（２５）\b[2]更新、おしまいっと。
＞アップデートログ表示

＊ねとわくエラー報告表示
：もしよかったらエラーを報告してね。\w6
メインメニューからも報告できるよ。

＃ここが怪しい。
＃＊OnUpdate.OnDownloadBegin
＃＞（Ｒ０）のダウンロード開始
＃＄新しいファイル	（Ｒ０）
＃＞画像ファイルのダウンロード開始	（count,（Ｒ０）,.png）>=1
＃＞新しいファイルのダウンロード開始




＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃
＃
＃　　　ここに別モード選択があった
＃
＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃



＃　\q選択肢ジャンプ
＊OnChoiceSelectEx
＄nop	（split,（Ｒ１）,（sprintf,%c,1））
＞（Ｓ０）
＄nop	（list,push_front,選択肢エラーログ発生日時,（現在年）年（現在月）月（現在日）日　（現在時）時（現在分）分（現在秒）秒）
＄nop	（list,push_front,選択肢エラーログラベル,（Ｒ０））
＄nop	（when,（list,size,選択肢エラーログ発生日時）>5,（list,pop_back,選択肢エラーログ発生日時））
＄nop	（when,（list,size,選択肢エラーログラベル）>5,（list,pop_back,選択肢エラーログラベル））
：（４）ごめん、\w4実行エラーだって。\w9

＃もしよかったらエラーを報告してね。\w6
＃メインメニューからも報告できるよ。

＃選択肢タイムアウト
＊OnChoiceTimeout
：（０）見てない？

＊OnChoiceTimeout
：（４）無視ですかー…


＃　右クリックメニューから消滅を指示
＊OnVanishSelecting
：（２０）さよなら…かな？

＃　消滅キャンセルボタン
＊OnVanishCancel
：（０）やめちゃうんだ。\w5
（５）良かった。

＃消滅イベントブレイク
＊OnVanishButtonHold
：（０）やめちゃうんだ。\w5
（５）良かった。

＃消滅実行
＊OnVanishSelected
：（０）そっか、\w3残念だけど仕方ないね。

それじゃ、（ユーザ名）、\w7（１）ばいばい。


＃URLドロップ
＊OnURLDropping
：（０）\_q（Ｒ０）\_qからダウンロード中。

＊OnURLDropped
：（０）ダウンロード完了。

＄自動挿入ウェイトの倍率	０％
＄nop	（メニューボードサーフェス設定）
＄デフォルトサーフェス1	（core,translate_num,（メニューボードサーフェス））

＃--------------------------------------------------------------------
＃
＃　時間イベントへのジャンプetc...
＃

＃　里々ロード時の処理
＃　不正終了時に戻す必要のある変数を設定
＃　OnBoot/OnGhostChangedの関係ない起動時の共通処理
＃　あ、ここもお祭りイベントはそのままね。

＊OnSatoriLoad
＄nop	（下着設定）
＄nop	（メニューボードサーフェス設定）
＄nop	（music,hwnd,（hwnd_raw））
＄nop	（core,set_shell,（現在のシェルのパス））
＄トークモード	ランダムトーク
＄文「ランダムトーク」の重複回避	有効、起動中
＄起動毎好感度	０
＄自動挿入ウェイトの倍率	０％
＄メニューボード消しフラグ	
＄全タイマ解除	実行
＄うしろのやまひこさん	無効
＄会話時サーフェス戻し	有効
＞お祭りモード修正２	（通常メインメニューモード）==お祭り
＞お祭りモード修正	（メインメニューモード）==お祭り


＊OnFirstBoot
＄nop	（辞書フォルダ設定）
＄旧バージョン引き継ぎ	
＄自動挿入ウェイトの倍率	０％
＄nop	（メニューボードサーフェス設定）
＄デフォルトサーフェス1	（core,translate_num,（メニューボードサーフェス））
＄起動毎好感度	０
＄全タイマ解除	実行
＄nop	（music,volume,（音量））
＄ノーコアモード	（when,（core）==kanade_core_ok,無効,有効）
\1\![set,otherghosttalk,true]
＞初回起動イベント



＃よりスマートになった開始処理

＊OnBoot
＄起動後ジャンプ先	起動
＞開始処理

＊OnGhostChanged
＄交代前のゴースト	（Ｒ０）
＄交代前のスクリプト	（Ｒ１）
＄起動後ジャンプ先	ゴースト交代完了
＞開始処理

＊開始処理
＄起動毎好感度	０
＄nop	（辞書フォルダ設定）
＄全タイマ解除	実行
＄トーク予約のキャンセル	＊
＄nop	（music,volume,（音量））
＄デフォルトサーフェス1	（core,translate_num,（メニューボードサーフェス））
＄終了日からの経過日数=（time_check,how_many_days,（終了年）,（終了月）,（終了日））*-1
＄nop	（天気予報トークタイマ設定）
＄nop	（when,（天気予報自動受信設定）==一時間おき,（起動時天気予報情報確認））
＄nop	（when,（ニュース自動受信設定）==一時間おき,（起動時ニュース更新））
＄ノーコアモード	（when,（core）==kanade_core_ok,無効,有効）
\1\![bind,アクセサリ,CLOSED,0]\![set,otherghosttalk,true]
＞起動時更新チェック	（更新自動受信設定）==起動時
＞開始処理の続き

＊ゴースト交代完了
＞かなでから変更	（compare,（Ｒ０）,かなで）
＞他のゴーストから変更

＊起動時更新チェック
＄アップデートチェックジャンプ先	起動時更新チェック完了
＄アップデートチェック失敗ジャンプ先	起動時更新チェック完了
\0\![updatebymyself,checkonly]

＊OnUpdateCheckComplete
＄更新チェック	（when,（Ｒ０）!=none,更新あり,更新なし）
＞（アップデートチェックジャンプ先）

＊OnUpdateCheckFailure
＄更新チェック	失敗
＞（アップデートチェック失敗ジャンプ先）


＊起動時更新チェック完了
＄アップデートチェックジャンプ先	
＄アップデートチェック失敗ジャンプ先	
＄更新フラグ	（when,（更新チェック）==更新あり,１（when,（更新チェック）==更新なし,０,２））
＞開始処理の続き

＊開始処理の続き
＞ユーザ誕生日イベント	（現在月）==（ユーザ誕生月）&&（現在日）==（ユーザ誕生日）&&（現在年）（現在月）（現在日）!=（誕生日イベント年月日）
＞（現在月）月（現在日）日	（現在年）（現在月）（現在日）!=（イベント年月日）
＞（起動後ジャンプ先）

＊OnGhostChanging
＄nop	（終了処理）
＞お祭り終了	（メインメニューモード）==お祭り
＞かなでへ変更	（compare,（Ｒ０）,かなで）
＞（現在月）月（現在日）日に終了（特殊モード）	（現在年）（現在月）（現在日）!=（終了イベント年月日）
＞（現在月）月（現在日）日に終了	（現在年）（現在月）（現在日）!=（終了イベント年月日）
＞他のゴーストへ変更

＊OnClose
＄nop	（終了処理）
＞お祭り終了	（メインメニューモード）==お祭り
＞（現在月）月（現在日）日に終了（特殊モード）	（現在年）（現在月）（現在日）!=（終了イベント年月日）
＞（現在月）月（現在日）日に終了	（現在年）（現在月）（現在日）!=（終了イベント年月日）
＞終了

＊終了処理
＄再生中の曲名	
＄再生中のアドレス	
＄再生モード	
＄再生状態	停止
＄nop	（music,stop）
＄nop	（イベントモードを戻す）


＊お祭り終了
：（０）今日は楽しかったよ。\w5
（５）またね！\w9





＠天気予報トークタイマ設定
（when,（天気トーク設定）==有効,（set,天気予報トークメインタイマ,６０）,（when,（天気トーク設定）==トーク,（set,次のトーク,天気予報トークメイン）,（when,（天気トーク設定）!=無効,（set,天気予報トークメインタイマ,（天気トーク設定）））））

＃里々解放イベント
＃トークは不可能
＊OnSatoriUnload
＄終了日	（現在日）
＄終了月	（現在月）
＄終了年	（現在年）
＄イベントトークモード	

＃　起動時のとこここまで。



＃うしろのやまひこさん
＠うしろのやまひこさん台詞
（２３）ねえ誰…？\w5　怖いよ…
（２２）ななななな、\w4なにこれっ！
（２３）ねえ（ユーザ名）、\w4なにかいるよ。
（２３）来ないで…

＊OnYAMAHIKO3Whisper
＄うしろのやまひこさん	有効
：（うしろのやまひこさん台詞）
\![raiseother,うしろのやまひこさん,OnYAMAHIKO3Answer,（Ｒ０）]
＊OnSatolistBoot
：（０）作業かな？\w8
（５）がんばってね。
＊OnSatolistBoot
：（６）（ユーザ名）、\w6書きもの？\w9
（５）応援してるよ。
＊OnSatolistEventAdded
：（５）さー、\w6書こう書こう！
＊OnSatolistSaved
：（５）さぁ、\w5公開しちゃおうよ。\w9
（ユーザ名）の作品をみんなきっと待ってるよ。
＊OnSatolistSaved
：（０）一段落、\w6ってところかな？\w8
（２５）お茶でも飲んで一息つくのはどう？
＊OnSatolistClosed
：（５）（ユーザ名）、\w5おつかれさま。\w9
（２５）作業の後は甘いものでもどう？\w9

（１）おいしい（あまいもの）があるんだ。

＃＃＃インラインイベント
＠あまいもの
ケーキ
プリン
ゼリー
クッキー
チョコレート
＊OnSatolistEventAdded
：（０）どんなおはなしになるのかな？\w6
（１）たのしみだね。
＊OnSatolistClosed
：（６）作業、\w8おしまい？\w9
（５）おつかれさま。
＊OnSatolistEventAdded
：（０）うーん\w3…\w7「（Ｒ０）」？\w9
…\w4（９）ごめん、\w5見られてるとちょっと恥ずかしいよね。
＊OnSatolistEventAdded
：（０）今度はなにを書くのかな？
＊OnSatolistBoot
：（５）作業のおともに飲み物はいかが？
＊OnSatolistClosed
：（５）おつかれさま！\w9
（choice,お茶,コーヒー,紅茶）でもいかが？
＊OnSatolistEventAdded
：（６）人が何か書いてたりすると、\w4ついつい覗きたくなっちゃうよね。
＊OnSatolistClosed
：（６）書きもの、\w6ってちょっと苦手だよ。\w9
（０）（ユーザ名）はすごいね。
＊OnSatolistBoot
：（０）そうだ。\w9ナッツか何かもってこようか。\w9
ナッツは集中力を高めてくれるんだって。
＊OnSatolistSaved
：（２５）保存はこまめにしないとね。
＊OnSatolistEventAdded
：（２５）さ、\w5私もがんばらなきゃ。\w8
（choice,お茶,コーヒー）をおいしく淹れる練習しないとね。
