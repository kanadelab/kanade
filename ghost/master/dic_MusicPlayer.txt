＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃
＃
＃　　　奏でる日常の音色　音楽再生機能の辞書
＃
＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃


＃ファイルドロップイベントをここで使用中。
＃他でイベントを使う場合は注意。

＃この機能ではファイル名やパスにカンマが含まれる可能性があるので、引数区切りを使用すること。
＃とりあえずドロップファイルは音楽だけ？

＊OnFileDropEx
＄引数区切り追加	（sprintf,%c,5）
＄ドロップ内容	（Ｒ０）
＄ドロップファイル数＝（count（b5）（Ｒ０）（b5）（バイト値，１））＋１
＞ドロップ内容が不正	（is_digit（b5）（ドロップファイル数））==0
＞ファイルがドロップされた

＊ドロップ内容が不正
：（４）うーん、読み込めないみたい。

＊ファイルがドロップされた
＞単数ファイルがドロップされた	（ドロップファイル数）==１
＞複数ファイルがドロップされた

＊単数ファイルがドロップされた
：（０）ん、これどうしよっか。

\![*]\q[音楽ファイルとして再生する,ドロップされたファイルを再生]
\![*]\q[音楽ファイルとしてプレイリストに追加する,プレイリスト複数追加]

\![*]\q[キャンセル,メインメニューを閉じる]

＊複数ファイルがドロップされた
：（０）これどうするの？

（ドロップファイル数）個のファイルがドロップされました。

\![*]\q[音楽ファイルとしてプレイリストに追加する,プレイリスト複数追加]

\![*]\q[キャンセル,メインメニューを閉じる]

＊ドロップされたファイルを再生
＄再生可判定	（music（バイト値,1）load（バイト値,1）（ドロップ内容））
＞ドロップされたファイルが再生不能	（再生可判定）==E_FAIL
＄再生中タイトル	
＄再生モード	外部単発再生
＄再生状態	再生
＄nop	（split（b5）（ドロップ内容）（b5）\）
＄再生中の曲名	（Ｓ（calc,（nop）−１））
＄nop	（music,play）
＞音楽再生メニュー


＊ドロップされたファイルが再生不能
：（４）うーん、再生できないみたい。


＊プレイリスト複数追加
＄nop	（list,byte1_split,プレイリスト追加リスト,（ドロップ内容））
＄nop	（list,clear,再生可能リスト）（list,clear,再生不可リスト）（list,clear,再生可能表示名リスト）（list,clear,再生不可表示名リスト）
＄nop	（loop,プレイリスト複数追加ループ,（list,size,プレイリスト追加リスト））
＄nop	（list,merge,音楽再生プレイリスト,再生可能リスト）
＄nop	（list,merge,音楽再生表示名リスト,再生可能表示名リスト）
＞プレイリスト追加完了

＊プレイリスト追加完了
（クイックセッション）\b[2]■プレイリスト追加完了

（when,（list,size,再生可能リスト）>0&&（list,size,再生不可リスト）>0,\s[0]いくつか再生できないファイルがあったみたい。,（when,（list,size,再生可能リスト）>0,\s[5]ファイルをプレイリストに追加したよ！,\s[4]どれも再生できないファイルだったみたい。））

（when,（list,size,再生可能リスト）>0,◆追加に成功したファイル\n（list,print,再生可能表示名リスト））
（when,（list,size,再生不可リスト）>0,◆追加に失敗したファイル\n（list,print,再生不可表示名リスト））\x


＃全部追加成功時	ファイルをプレイリストに追加したよ！
＃部分追加成功時	いくつか再生できないファイルが有ったみたい。
＃全部追加失敗時	どれも再生できないファイルみたい。


＊プレイリスト複数追加ループ
＄nop	（call,プレイリスト複数追加選別関数,（list,get,プレイリスト追加リスト,（calc,（プレイリスト複数追加ループカウンタ）-1）））

＃　0:パス
＊プレイリスト複数追加選別関数
＄再生可否情報	（music（b5）load（b5）（Ａ０））
＞追加に失敗したファイルの表示名を登録	（再生可否情報）==E_FAIL
＄ファイルタグ	（Ｓ０）
＞タグデータを表示名として使用	（変数「ファイルタグ」の存在）==1
＞ファイル名を表示名として使用

＊タグデータを表示名として使用
＄nop	（list（b5）push_back（b5）再生可能リスト（b5）（Ａ０））
＄nop	（when（b5）（変数「ファイルタグ」の存在）==1（b5）（list（b5）push_back（b5）再生可能表示名リスト（b5）（Ｓ０））（b5））

＊ファイル名を表示名として使用
＄nop	（list（b5）push_back（b5）再生可能リスト（b5）（Ａ０））
＄nop	（set（b5）分割数（b5）（split（b5）（Ａ０）（b5）\））（split（b5）（Ｓ（calc（b5）（分割数）-1））（b5）.）（list（b5）push_back（b5）再生可能表示名リスト（b5）（Ｓ０））

＊追加に失敗したファイルの表示名を登録
＄nop	（list（b5）push_back（b5）再生不可リスト（b5）（Ａ０））
＄nop	（set（b5）分割数（b5）（split（b5）（Ａ０）（b5）\））（split（b5）（Ｓ（calc（b5）（分割数）-1））（b5）.）（list（b5）push_back（b5）再生不可表示名リスト（b5）（Ｓ０））

＃　b5セパレートにすること。

＊OnSetMusicVolumeToMusicMenu
（music（b5）volume（b5）（Ｒ０））
＞音楽再生メニュー

＊OnSetMusicVolumeToMusicTips
（music（b5）volume（b5）（Ｒ０））
＄音量	（music,volume）
＄音量パーセンテージ＝（音量）/100
\1（Tips音楽再生コントロール）\x

＃　再生中の時に「次の曲」「停止」と「再生中の曲名」を表示する。

＊音楽再生メニュー
＄引数区切り追加	（sprintf,%c,5）
＄音量	（music,volume）
＄音量パーセンテージ＝（音量）/100
（クイックセッション）\0\b[2]\*■音楽再生メニュー

◆Play Music
\![*]\q[プレイリストを見る,音楽再生プレイリスト表示]
\![*]\q[シャッフル再生,音楽再生シャッフル再生]
\![*]\q[順番に再生,OnPlayListSinglePlay,0]

（when,（変数「再生中の曲名」の存在）==1,◆Now Playing（when,（再生モード）==シャッフル再生, -> Shuffle,（when,（再生モード）==外部単発再生, -> File））\n（再生中の曲名））

（when,（再生状態）==再生,◆Control\n（when,（再生モード）==外部単発再生,（外部音楽再生メニューコントロール）,（音楽再生メニューコントロール））,（when,（再生状態）==一時停止,◆Control\n（音楽一時停止メニューコントロール）,\n\n））

◆Volume
Min （call,音楽再生メニュー音量ボタン,0,10）（call,音楽再生メニュー音量ボタン,10,20）（call,音楽再生メニュー音量ボタン,20,30）（call,音楽再生メニュー音量ボタン,30,40）（call,音楽再生メニュー音量ボタン,40,50）φ
（call,音楽再生メニュー音量ボタン,50,60）（call,音楽再生メニュー音量ボタン,60,70）（call,音楽再生メニュー音量ボタン,70,80）（call,音楽再生メニュー音量ボタン,80,90）（call,音楽再生メニュー音量ボタン,90,100）（call,音楽再生メニュー音量ボタン,100,110） Max

◆Mode
\![*]\q[再生単位の変更　　　　　【（音楽再生単位）】,OnSwitchReturn,音楽再生単位,１曲を,プレイリストを,音楽再生メニュー]
\![*]\q[ループ設定の変更　　　　【（音楽再生ループ設定）】,OnSwitchReturn,音楽再生ループ設定,ループする,ループしない,音楽再生メニュー]


\![*]\q[とじる,メインメニューを閉じる]
\![*]\q[もどる,メインメニュー]

＠音楽再生メニュー音量ボタン
\q[（when,（音量パーセンテージ）>=（Ａ０）&&（音量パーセンテージ）<（Ａ１）,■,□）,OnSetMusicVolumeToMusicMenu,（calc,（Ａ０）*100）]

＠音楽再生Tips音量ボタン
\q[（when,（音量パーセンテージ）>=（Ａ０）&&（音量パーセンテージ）<（Ａ１）,■,□）,OnSetMusicVolumeToMusicTips,（calc,（Ａ０）*100）]

＊音楽再生メニュー一時停止
＄nop	（music,pause）
＄再生状態	一時停止
＞音楽再生メニュー

＊音楽再生メニュー再生
＄nop	（music,play）
＄再生状態	再生
＞音楽再生メニュー

＊音楽再生メニュー停止
＄nop	（music,stop）
＄再生中の曲名	
＄再生中のアドレス	
＄再生状態	停止
＞音楽再生メニュー

＊音楽再生メニュースキップ
＄nop	（OnDSAPlayFinish）
＞音楽再生メニュー

＃一時停止　スキップ　停止
＠音楽再生メニューコントロール
\![*]\q[一時停止,音楽再生メニュー一時停止]　\![*]\q[曲をスキップ,音楽再生メニュースキップ]　\![*]\q[停止,音楽再生メニュー停止]

＠音楽一時停止メニューコントロール
\![*]\q[再開,音楽再生メニュー再生]　\![*]\q[停止,音楽再生メニュー停止]

＠外部音楽再生メニューコントロール
\![*]\q[一時停止,音楽再生メニュー一時停止]　\![*]\q[停止,音楽再生メニュー停止]

＊音楽再生プレイリスト表示
（クイックセッション）\0\b[2]\*■音楽再生メニュー - プレイリスト

（list（b5）open_playlist（b5）音楽再生プレイリスト（b5）音楽再生表示名リスト）
\![*]\q[プレイリストを編集する,音楽再生プレイリスト編集]
\![*]\q[もどる,音楽再生メニュー]
\![*]\q[とじる,メインメニューを閉じる]

＊音楽再生プレイリスト編集
（クイックセッション）\0\b[2]\*■音楽再生メニュー - プレイリスト編集

（list,arrange_playlist,音楽再生表示名リスト）
\![*]\q[プレイリストをクリア,音楽再生プレイリストクリア]

\![*]\q[もどる,音楽再生プレイリスト表示]
\![*]\q[とじる,メインメニューを閉じる]

＊音楽再生プレイリストクリア
＄nop	（list,clear,音楽再生表示名リスト）（list,clear,音楽再生プレイリスト）
＞音楽再生プレイリスト編集


＊OnPlayListSinglePlay
＄引数区切り追加	（sprintf,%c,5）
＄再生中の曲名	（list,get,音楽再生表示名リスト,（Ｒ０））
＄再生中のアドレス	（list,get,音楽再生プレイリスト,（Ｒ０））
＄再生モード	連続再生
＄再生状態	再生
＄再生中の番号	（Ｒ０）
＄nop	（music（b5）load（b5）（再生中のアドレス））
＄nop	（music,play）
＞音楽再生メニュー

＃　再生終了イベント
＊OnDSAPlayFinish
＄再生中の曲名	
＄再生中のアドレス	
＞（再生モード）次曲

＊連続再生次曲
＞再生終了	（音楽再生単位）==１曲を&&（音楽再生ループ設定）==ループしない
＞同じ曲をループ	（音楽再生単位）==１曲を&&（音楽再生ループ設定）==ループする
＄再生中の番号	（calc,（再生中の番号）+1）
＞プレイリストをループ	（再生中の番号）>=（list,size,音楽再生表示名リスト）&&（音楽再生単位）==プレイリストを&&（音楽再生ループ設定）==ループする
＞再生終了	（再生中の番号）>=（list,size,音楽再生表示名リスト）
＄再生中の曲名	（list,get,音楽再生表示名リスト,（再生中の番号））
＄再生中のアドレス	（list,get,音楽再生プレイリスト,（再生中の番号））
＄nop	（music（b5）load（b5）（再生中のアドレス））
＄nop	（music,play）

＊同じ曲をループ
＄再生中の曲名	（list,get,音楽再生表示名リスト,（再生中の番号））
＄再生中のアドレス	（list,get,音楽再生プレイリスト,（再生中の番号））
＄nop	（music（b5）load（b5）（再生中のアドレス））
＄nop	（music,play）

＊プレイリストをループ
＄再生中の番号	0
＞同じ曲をループ

＊外部単発再生次曲
＞再生終了


＊再生終了
＄nop	（music,stop）
＄再生状態	停止



＊音楽再生シャッフル再生
＄nop	（list,clear,音楽再生シャッフルリスト）
＄nop	（loop,シャッフル再生リスト作成,（list,size,音楽再生表示名リスト））
＄再生モード	シャッフル再生
＄再生状態	再生
＄シャッフル番号	（乱数0〜（calc,（list,size,音楽再生シャッフルリスト）-1））
＄再生中の番号	（list,get,音楽再生シャッフルリスト,（シャッフル番号））
＄nop	（list,erase,音楽再生シャッフルリスト,（シャッフル番号））
＄再生中の曲名	（list,get,音楽再生表示名リスト,（再生中の番号））
＄再生中のアドレス	（list,get,音楽再生プレイリスト,（再生中の番号））
＄nop	（music（b5）load（b5）（再生中のアドレス））
＄nop	（music,play）
＞音楽再生メニュー

＊シャッフル再生リスト作成
＄nop	（list,push_back,音楽再生シャッフルリスト,（calc,（シャッフル再生リスト作成カウンタ）-1））

＊シャッフル再生次曲
＞再生終了	（list,size,音楽再生シャッフルリスト）<=0
＄シャッフル番号	（乱数0〜（calc,（list,size,音楽再生シャッフルリスト）-1））
＄再生中の番号	（list,get,音楽再生シャッフルリスト,（シャッフル番号））
＄nop	（list,erase,音楽再生シャッフルリスト,（シャッフル番号））
＞シャッフル再生次曲	（再生中の番号）>=（list,size,音楽再生表示名リスト）
＄再生中の曲名	（list,get,音楽再生表示名リスト,（再生中の番号））
＄再生中のアドレス	（list,get,音楽再生プレイリスト,（再生中の番号））
＄nop	（music（b5）load（b5）（再生中のアドレス））
＄nop	（music,play）



＃R0:オペレーション
＃R1:位置
＊OnArrangePlaylist
＄nop	（list,（Ｒ０）,音楽再生表示名リスト,（Ｒ１））（list,（Ｒ０）,音楽再生プレイリスト,（Ｒ１））
＞音楽再生プレイリスト編集

＃ウインドウハンドル
＊hwnd
＄nop	（music,hwnd,（Ｒ０））


＃メニューボード上の操作パネル
＃必要なのは　再生・停止・一時停止　と　音量　と　再生中の曲名　かな　あとプレイリストを開く？


＊Tips音楽再生コントロール
\c（クイックセッション）\1\![*]\q[音楽再生コントロール,Tips表示設定]
（when,（再生状態）==再生,（when,（再生モード）==外部単発再生,（外部音楽再生Tipsコントロール）,（音楽再生Tipsコントロール））,（when,（再生状態）==一時停止,（音楽一時停止Tipsコントロール）））

◆Volume
Min （call,音楽再生Tips音量ボタン,0,10）（call,音楽再生Tips音量ボタン,10,20）（call,音楽再生Tips音量ボタン,20,30）（call,音楽再生Tips音量ボタン,30,40）（call,音楽再生Tips音量ボタン,40,50）φ
（call,音楽再生Tips音量ボタン,50,60）（call,音楽再生Tips音量ボタン,60,70）（call,音楽再生Tips音量ボタン,70,80）（call,音楽再生Tips音量ボタン,80,90）（call,音楽再生Tips音量ボタン,90,100）（call,音楽再生Tips音量ボタン,100,110） Max


＃一時停止　スキップ　停止
＠音楽再生Tipsコントロール
\![*]\q[一時停止,音楽再生メニュー一時停止]　\![*]\q[曲をスキップ,音楽再生メニュースキップ]　\![*]\q[停止,音楽再生メニュー停止]

＠音楽一時停止Tipsコントロール
\![*]\q[再開,音楽再生メニュー再生]　\![*]\q[停止,音楽再生メニュー停止]

＠外部音楽再生Tipsコントロール
\![*]\q[一時停止,音楽再生メニュー一時停止]　\![*]\q[停止,音楽再生メニュー停止]

＊音楽再生Tips一時停止
＄nop	（music,pause）
＄再生状態	一時停止
＞Tips音楽再生コントロール

＊音楽再生Tips再生
＄nop	（music,play）
＄再生状態	再生
＞Tips音楽再生コントロール

＊音楽再生Tips停止
＄nop	（music,stop）
＄再生中の曲名	
＄再生中のアドレス	
＄再生状態	停止
＞Tips音楽再生コントロール

＊音楽再生Tipsスキップ
＄nop	（OnDSAPlayFinish）
＞Tips音楽再生コントロール