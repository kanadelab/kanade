＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃
＃
＃　　　奏でる日常の音色　タイマー辞書
＃
＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃

＊ユーザ「　タイマー
＞タイマー機能

＊タイマー機能
＄タイマー合計値＝（タイマー設定時間）＊３６００＋（タイマー設定分）＊６０＋（タイマー設定秒）
＄タイマー合計時間＝（タイマー合計値）/３６００
＄nop＝（タイマー合計値）％３６００
＄タイマー合計分＝（nop）/６０
＄タイマー合計秒＝（nop）％６０
＄タイマー残り値＝（when,（変数「タイマー一時停止時間」の存在）==1,（タイマー一時停止時間）,（タイマー終了タイマ））
＄タイマー残り時間＝（タイマー残り値）/３６００
＄nop＝（タイマー残り値）％３６００
＄タイマー残り分＝（nop）/６０
＄タイマー残り秒＝（nop）％６０
：（クイックセッション）\b[2]\*◆タイマー

◆タイマーの設定
\![*]\q[（タイマー設定時間）時間,OnOpenTimerInputBox,時間]　　\![*]\q[リセット,OnResetTimer,時間]　\q[　　＋　　,OnPlusTimer,時間]　\q[　　−　　,OnMinusTimer,時間]
\![*]\q[（タイマー設定分）分,OnOpenTimerInputBox,分]　　　\![*]\q[リセット,OnResetTimer,分]　\q[　　＋　　,OnPlusTimer,分]　\q[　　−　　,OnMinusTimer,分]
\![*]\q[（タイマー設定秒）秒,OnOpenTimerInputBox,秒]　　　\![*]\q[リセット,OnResetTimer,秒]　\q[　　＋　　,OnPlusTimer,秒]　\q[　　−　　,OnMinusTimer,秒]

（when,（変数「タイマー一時停止時間」の存在）==1,（タイマー一時停止中）,（when,（タイマー終了タイマ）>0,（タイマーカウント中）,（タイマー準備中）））
◆通知音
\![*]\q[通知:（タイマー通知音設定）,タイマー通知音設定変更]
\![*]\q[通知音を鳴らしてみる,タイマー通知音テスト]

◆最近使った設定（when,（list,size,タイマー履歴）<=0,\n履歴はありません。）
（call,タイマー履歴表示,0）
（call,タイマー履歴表示,1）
（call,タイマー履歴表示,2）
（call,タイマー履歴表示,3）
（call,タイマー履歴表示,4）


\![*]\q[もどる,メニュー表示]
\![*]\q[とじる,メインメニューを閉じる]

＠タイマー履歴表示
（when,（list,size,タイマー履歴）>（Ａ０）,（call,タイマー履歴ボタン,（Ａ０）））

＠タイマー履歴ボタン
\![*]\q[（set,nop,（split,（list,get,タイマー履歴,（Ａ０））,.））（Ｓ０）時間　（Ｓ１）分　（Ｓ２）秒,OnTimerSetFromList,（Ａ０）]

＊OnTimerSetFromList
＄nop	（split,（list,get,タイマー履歴,（Ｒ０））,.）
＄タイマー設定時間	（Ｓ０）
＄タイマー設定分	（Ｓ１）
＄タイマー設定秒	（Ｓ２）
＞タイマー機能

＊タイマー通知音設定変更
＄タイマー通知音設定	（when,（タイマー通知音設定）==通知音を鳴らす,通知音を鳴らさない,通知音を鳴らす）
＞タイマー機能

＊タイマー通知音テスト
\0\n\n効果音を鳴らしてるよ。\_v[saori\timer.mp3]\w9\w9\w9\w9\w9\c
＞タイマー機能


＊OnOpenTimerInputBox
＄設定変数	（Ｒ０）
：（クイックセッション）\![open,inputbox,OnSetTimer]\s[0]タイマーにセットする「（Ｒ０）」を数字で入力してね。

＊OnSetTimer
＄タイマー設定（設定変数）	（Ｒ０）
＞タイマー機能

＊OnResetTimer
＄タイマー設定（Ｒ０）	０
＞タイマー機能

＊OnPlusTimer
＄タイマー設定（Ｒ０）＝（タイマー設定（Ｒ０））+１
＞タイマー機能

＊OnMinusTimer
＄タイマー設定（Ｒ０）＝（タイマー設定（Ｒ０））−１
＄タイマー設定（Ｒ０）＝（when,（タイマー設定（Ｒ０））<０,０,（タイマー設定（Ｒ０）））
＞タイマー機能

＊タイマーカウントダウン開始
＄タイマー終了タイマ	（タイマー合計値）
＄nop	（list,push_front,タイマー履歴,（タイマー設定時間）.（タイマー設定分）.（タイマー設定秒））
＄nop	（when,（list,size,タイマー履歴）>5,（list,pop_back,タイマー履歴））
＞タイマー機能

＊タイマー準備中
：◆タイマー準備中
（タイマー合計時間）時間　（タイマー合計分）分　（タイマー合計秒）秒
\![*]\q[タイマースタート,タイマーカウントダウン開始]


＊タイマーカウント中
：◆タイマーカウント中
残り　（タイマー残り時間）時間　（タイマー残り分）分　（タイマー残り秒）秒　くらい
\![*]\q[キャンセル,タイマーキャンセル]　　\![*]\q[一時停止,タイマー一時停止]

＊タイマー一時停止中
：◆タイマー一時停止中
（タイマー残り時間）時間　（タイマー残り分）分　（タイマー残り秒）秒
\![*]\q[再開,タイマー再開]　　\![*]\q[キャンセル,タイマーキャンセル]

＊タイマー再開
＄タイマー終了タイマ	（タイマー一時停止時間）
＄タイマー一時停止時間	
＞タイマー機能

＊タイマー一時停止
＄タイマー一時停止時間	（タイマー終了タイマ）
＞タイマー機能

＊タイマーキャンセル
＄タイマー一時停止時間	
＄タイマー終了タイマ	０	
＞タイマー機能

＊タイマー終了
：（５）（when,（タイマー通知音設定）==通知音を鳴らす,（タイマー通知音再生））タイマー終わったよ！\x

＠タイマー通知音再生
\_v[saori\timer.mp3]